/*
 * Copyright 2018 Manuel Wrage
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jakewharton.butterknife'

group='com.github.ivianuu'

android {
    compileSdkVersion 27

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 27
    }
    androidExtensions {
        experimental = true
    }
    kapt {
        correctErrorTypes = true
    }
}

dependencies {
    // Android Support
    api "com.android.support:appcompat-v7:27.1.1"
    api "com.android.support:cardview-v7:27.1.1"
    api "com.android.support.constraint:constraint-layout:1.1.0"
    api "com.android.support:design:27.1.1"
    api "com.android.support:palette-v7:27.1.1"
    api "com.android.support:preference-v14:27.1.1"

    // Arch Components
    api "android.arch.lifecycle:extensions:1.1.1"

    // Dagger
    api "com.google.dagger:dagger:2.15"
    api "com.google.dagger:dagger-android:2.15"
    api "com.google.dagger:dagger-android-support:2.15"
    kapt "com.google.dagger:dagger-compiler:2.15"
    kapt "com.google.dagger:dagger-android-processor:2.15"

    // Epoxy
    api "com.airbnb.android:epoxy:2.10.0"
    kapt "com.airbnb.android:epoxy-processor:2.10.0"

    // Kommon Extensions
    api 'com.github.IVIanuu:KommonExtensions:838e6ac71f'

    // Kotlin
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Kt android
    api 'androidx.core:core-ktx:0.3'

    // Material Dialogs
    api "com.afollestad.material-dialogs:core:0.9.6.0"
    api "com.afollestad.material-dialogs:commons:0.9.6.0"

    // Rx Activity Result
    api 'com.github.IVIanuu:RxActivityResult:3a17011d75'

    // RxJava
    api "io.reactivex.rxjava2:rxjava:2.1.12"
    api "io.reactivex.rxjava2:rxandroid:2.0.2"

    // RxKotlin
    api "io.reactivex.rxjava2:rxkotlin:2.2.0"

    // RxPermissions
    api 'com.github.IVIanuu:RxPermissions:dc2b269921'

    // RxPreferences
    api "com.f2prateek.rx.preferences2:rx-preferences:2.0.0-RC3"

    // Timber
    api "com.jakewharton.timber:timber:4.6.0"
    api "com.github.ajalt:timberkt:1.3.0"

    // Traveler
    api 'com.github.IVIanuu.Traveler:traveler:b93a67bde1'
    api 'com.github.IVIanuu.Traveler:traveler-keys:b93a67bde1'
}

// build a jar with source files
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError  false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
